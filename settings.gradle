pluginManagement {
    plugins {
        id 'org.jetbrains.kotlin.jvm' version '2.0.10'
    }
}

include ':FtcRobotController'

def team = null
File propertiesFile = file('my.properties')
if (propertiesFile.exists()) {
    Properties properties = new Properties()
    properties.load(propertiesFile.newDataInputStream())
    team = properties.getProperty('team')
    if (team != null) {
        team = team[0].toUpperCase() + team[1..-1].toLowerCase() // Make first character upper-case
        if ((team != 'Team417') && (team != 'Team6220') && (team != 'Team8923')) {
            println("ERROR: The team property in 'my.properties' is set to '" + team + "', " +
                    "it needs to be Team417, Team6220 or Team8923")
        }
    }
}

if (team != null) {
    println(team + " rules!\n")
    include team
} else {
    println("Hi there Swerve person, please create the 'my.properties' file and add your team!\n")
    include ':Team417'
    include ':Team6220'
}

include ':WilyWorks'
include ':WilyCore'
